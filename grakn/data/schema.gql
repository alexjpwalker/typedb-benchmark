define

#######################################################################
# Attributes
#######################################################################

name sub attribute, abstract,
    value string;

location-name sub name;

organisation-name sub name, abstract;
company-name sub organisation-name;
#university-name sub organisation-name;
#charity-name sub organisation-name;

company-number sub attribute,
    value long;

forename sub name;

surname sub name;

product-name sub name;

#service-name sub name;

date-of-event sub attribute,
    abstract,
    value datetime;

date-of-incorporation sub date-of-event;

start-date sub date-of-event;

end-date sub date-of-event;

date-of-birth sub date-of-event;

date-of-marriage sub date-of-event;  # TODO unused

relocation-date sub date-of-event;

gender sub attribute,
    value string;

age sub attribute,
    value long;

language sub attribute,
    value string,
    regex "^(English|German|Portugese|Afrikaans|Mandarin)$";

#content-language sub attribute,
#    value string;

#num-characters sub attribute,
#    value long;

text-content sub attribute, abstract,
    value string;
#    owns content-language, # Seems to break grakn when querying for this
#    owns num-characters;

contract-content sub text-content;

contracted-hours sub attribute,
    value double;

wage-value sub attribute,
    value double;

currency-code sub attribute,
    value string,
    regex "^(USD|GBP|EUR|BRL|ZAR|RMB|AUD)$";

currency sub attribute,
    value string,
    regex "^(American Dollar|Great British Pound|Euro|Brazilian Real|South African Rand|Renminbi|Australian Dollar)$",
    owns currency-code;

value sub attribute,
    value double;

date-signed sub attribute,
    value datetime;

email sub attribute,
    value string;

identifier sub attribute, abstract,
    value long;

marriage-id sub identifier;

#service-id sub identifier;

product-barcode sub identifier;

description sub attribute, abstract,
    value string;

product-description sub description;

product-quantity sub attribute,
    value long;

#service-description sub description;

is-taxable sub attribute,
    value boolean;

is-current sub attribute,
    value boolean;

#######################################################################
# Entities, Relations and Rules, by approximate category
#######################################################################

# --------------------------------------------------------
# Locations
# --------------------------------------------------------

location sub entity,
    abstract,
    owns location-name @key,
    plays location-hierarchy:superior,
    plays location-hierarchy:subordinate,
    plays relocation:previous-location,
    plays relocation:new-location,
    plays locates:location,
    plays born-in:place-of-birth;  # TODO Can we avoid using this but also avoid validation errors?

city sub location,
    plays residency:location;

country sub location,
    owns currency,
    owns language,
    plays incorporation:incorporating;

continent sub location,
    plays produced-in:continent;

location-hierarchy sub relation,
    relates superior,
    relates subordinate;

locates sub relation,
    relates location,
    relates located;

rule location-hierarchy-transitivity:
when {
    $lh1(superior: $a, subordinate: $b) isa location-hierarchy;
    $lh2(superior: $b, subordinate: $c) isa location-hierarchy;
} then {
    (superior: $a, subordinate: $c) isa location-hierarchy;
};

# TODO: uncomment once reasoning is enabled
# Transitive group2 membership
# Here we realise that it's common to say that when something takes place in a location it also takes place in the superior places.
# This will lead to many rules of this kind. Can this be generified, and should it be?
#rule locates-transitive-with-location-hierarchy:
#when {
#    $loc(located: $thing, location: $a) isa locates;
#    $lh2(superior: $b, subordinate: $a) isa location-hierarchy;
#} then {
#    (located: $thing, location: $b) isa locates;
#};

# --------------------------------------------------------
# Currency
# --------------------------------------------------------

rule transaction-currency-is-that-of-the-country:
when {
    $transaction isa transaction;
    $l(located: $transaction, location: $transaction-location) isa locates;
    $transaction-location isa country, has currency $currency;
} then {
    $transaction has $currency;
};

# --------------------------------------------------------
# Friends and Family
# --------------------------------------------------------

person sub entity,
    owns email @key,
    owns forename,
    owns surname,
    owns gender,
    owns date-of-birth,
    owns age,
    plays employment:employee,
#    plays volunteering:volunteer,
#    plays studentship:student,
    plays parentship:parent,
    plays parentship:child,
    plays marriage:husband,
    plays marriage:wife,
    plays friendship:friend,
    plays born-in:child,
    plays residency:resident,
    plays relocation:relocated-person;

parentship sub relation,
    relates parent,  # Could have two parents playing parent in a single parentship for interesting cardinality
    relates child;

friendship sub relation,
    owns start-date,
    owns end-date,
    relates friend; # Could be N-ary, though in a real-world context this is imperfect

marriage sub relation,
    owns marriage-id @key,
    owns date-of-marriage,  # TODO unused
    relates husband,
    relates wife,
    plays locates:located;

# --------------------------------------------------------
# Individuals
# --------------------------------------------------------

born-in sub locates,
    relates place-of-birth as location,
    relates child as located;

residency sub relation,
    owns start-date,
    owns end-date,
    owns is-current,
    relates resident,
    relates location;

relocation sub relation,
    owns relocation-date,
    relates previous-location,
    relates new-location,
    relates relocated-person;

# Causes of residency
# - born in a location
# - move to a location
# - can be inferred from where someone works or studies at school or university
# TODO: uncomment once reasoning is enabled
#rule born-in-location-implies-residency:
#when {
#    $bi(place-of-birth: $location, child: $person) isa born-in;
#} then {
#    (resident: $person, location: $location) isa residency;
#};

# TODO: uncomment once reasoning is enabled
# This rule can be triggered by a person moving back to their hometown having moved away, and add an incorrect start-date
#rule born-in-location-implies-residency-date:
#when {
#    $bi(place-of-birth: $location, child: $person) isa born-in;
#    $person isa person, has date-of-birth $dob;
#    $r(resident: $person, location: $location) isa residency;
#} then {
#    $r has $dob;
#};

# TODO: uncomment once reasoning is enabled
#rule person-relocating-adds-new-residency:
#when {
#    $rel(relocated-person: $person, new-location: $location) isa relocation;
#} then {
#    (resident: $person, location: $location) isa residency;
#};

# TODO: uncomment once reasoning is enabled
#rule person-relocating-ends-old-residency:
#when {
#    $rel(relocated-person: $person, previous-location: $location) isa relocation, has relocation-date $rel-date;
#    $old-residency(resident: $person, location: $location) isa residency;
#} then {
#    $old-residency has $rel-date;
#};

# TODO: uncomment once reasoning is enabled
#rule residency-without-end-date-is-current:
#when {
#    $res(resident: $person, location: $location) isa residency;
#    not {
#        $res has end-date $rel-date;
#    };
#} then {
#    $res has is-current true;
#};

rule label-non-current-residency:
when {
    $res isa residency, has end-date $end-date;
} then {
    $res has is-current false;
};

# --------------------------------------------------------
# Organisational Structure
# --------------------------------------------------------

organisation sub entity;

company sub organisation,
    # We may want to say `owns company-name as organisation-name;`
    owns company-name @key,
    owns company-number @key,
    plays employment:employer,
    plays transaction:seller,
    plays transaction:buyer,
    plays incorporation:incorporated;

incorporation sub relation,
    owns date-of-incorporation,
    relates incorporated,
    relates incorporating;

#university sub organisation,
#    owns university-name,
#    plays employment:employer,
#    plays studentship:school,
#    plays transaction:seller,
#    plays transaction:buyer;

#charity sub organisation,
#    owns charity-name,
#    plays volunteering:cause,
#    plays transaction:buyer;

membership sub relation,
#    owns identifier @key,
    owns start-date,
    owns end-date,
    relates member,
    relates group2;

employment sub membership,
    # owns employment-id as membership-id @key, # We might want to say this?
    relates employee as member,
    relates employer as group2,
    # relates contract,
    # relates profession,
    # relates wage,  # https://github.com/graknlabs/grakn/issues/5553
    # For wages in different currencies, either:
    # - subtype for the different currencies,
    # - add currency as attribute to implicit relation,
    # - model the wage as an entity,
    # The attribute on an implicit relation is a pattern we would like to stress-test

    owns contracted-hours,
    # Nested relation
    plays locates:located;

wage sub entity,
#    plays employment:wage,
    owns wage-value,
    owns currency;

#studentship sub membership,
#    relates student as member,
#    relates school as group2;

#volunteering sub membership,
#    relates volunteer as member,
#    relates cause as group2;

#occupation sub entity,
#    owns name @key;
#    plays employment:profession;

legal-document sub entity, abstract,
    owns date-signed,  # TODO unused
    owns text-content;

employment-contract sub legal-document, abstract,
    # plays employment:contract,
    owns contract-content,
    owns contracted-hours;

# --------------------------------------------------------
# Organisational Trade
# --------------------------------------------------------

transaction sub relation,
    relates buyer,
    relates seller,
    relates merchandise,
    owns currency,
    owns value,
    owns product-quantity,
    owns is-taxable,
    plays locates:located;

product sub entity,
    owns product-barcode @key,
    owns product-name,
    owns product-description,
    plays transaction:merchandise,
    plays produced-in:product;

produced-in sub relation,
    relates product,
    relates continent;

#service sub entity,
#    owns service-id @key,
#    owns service-name,
#    owns service-description,
#    plays transaction:merchandise;

employment sub membership,
    relates contract,
#    relates employment_profession,
    relates wage;

#occupation sub entity,
#   plays employment_profession;

employment-contract sub legal-document,
    plays employment:contract;

wage sub entity,
    plays employment:wage;

#contract-has-language-of-location-is-there-is-only-one-language sub rule,
#when {
#    $emp(employment_contract: $contract) isa employment;
#    $contract isa employment-contract, has contract-content $content;
#    ($emp, $location1) isa locates;
#    $location1 has language $lang1;
#    not {
#        ($emp, $location2) isa locates;
#        $location2 has language $lang2;
#        $lang1 !== $lang2;
#    };
#}, then {
#    $content has content-language $lang1;
#};

#rule contracted-hours-apply-to-employment:
#when {
#    $emp(contract: $c) isa employment;
#    $c isa employment-contract, has contracted-hours $hours;
#} then {
#    $emp has $hours;
#};
